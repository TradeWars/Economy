buildscript {
    repositories {
        jcenter()
    }

    ext.kotlin_version = '1.2.51'
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.+'
    }
}

wrapper {
    gradleVersion = '4.9'
}

//IDEs
apply plugin: 'idea'

// Kotlin
apply plugin: 'kotlin'

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

apply plugin: 'application'

mainClassName = 'org.tradewars.economy.MainKt'

repositories {
    jcenter()
}

dependencies {
    //Kotlin stdlib
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    //Json library
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.+'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    //Dependencies for testing
    testCompile("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.2.0")
}

//Test coverage tool
apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.1'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.setEnabled(true)

        csv.setEnabled(false)
        html.setEnabled(false)
    }
}

//Builds a selfcontained (fat) jar of the project.
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    dependsOn "test"

    baseName = 'Economy'
    classifier = null
}